# What is Pure?

Pure takes the hierarchical structure and contextualization that is ubiquitous in modern `html` user interfaces, and extends the concept to non-frontend domains.

Pure has been designed to be very broad in terms of its applicability. On the browser side, pure, declarative views combined with [Elm](http://elm-lang.org)-style applications with [React](http://reactjs.org)-style components allow you to near-directly transliterate code from Elm, from generic templates, or from react components, or any combination thereof. On the server side, Pure relies on the same constructs to build dynamic, hierarchical applications, so you only have to learn one framework to build frontend, backend, and intermediate appliations.

Simply, Pure is a [Haskell](https://haskell.org) framework that admits a unified development architecture for interactive systems. Pure can be used to build web pages, servers and services, as well as desktop and mobile applications.

Pure is compiled to JavaScript with [GHCJS](https://github.com/ghcjs/ghcjs) to build web, desktop, and mobile applications, or natively compiled with [GHC](https://haskell.org/ghc) to build web servers and networked services.

## A Simple Example

If you're only interested in webapp development, Pure hits just the right combination of power to weight; all of the power of a full multithreaded GHC runtime system with its exquisite type system combined with simple declarative view code that can be seamlessly transitioned into complex, stateful, component-ized views. 

Here's an example of a simple sateful counter using [pure-state](/doc/pure-state/latest):

```haskell
module Main where

import Pure

main = inject body counter

counter = runPure 0 $ do
  n <- get 

  dec <- embedPure (modify pred)
  inc <- embedPure (modify succ)

  return $ 
    Div <||>
      [ Button <| OnClick (const dec) |> [ "-" ]
      , Div <||> [ text n ]
      , Button <| OnClick (const inc) |> [ "+" ]
      ]
```

Or in component style using basic [pure components](/doc/pure-core/latest/Pure.Data.View/data%20Comp):

```haskell
module Main where

import Pure

main = inject body (counter ())

counter = Component $ \self -> 
  let
    dec = modify_ self $ \_ -> pred
    inc = modify_ self $ \_ -> succ
  in
    def
      { construct = return 0
      , render = \_ n ->
        Div <||>
          [ Button <| OnClick (const dec) |> [ "-" ]
          , Div <||> [ text n ]
          , Button <| OnClick (const inc) |> [ "+" ]
          ]
      }
```

Or in the style of [The Elm Architecture](https://guide.elm-lang.org/architecture/) with message-based state management using [pure-elm](/doc/pure-elm/latest):

```haskell
import Pure.Elm

main = inject body counter

counter :: View
counter = run app (App [] [] [] 0 update view)

data Msg = Increment | Decrement

update Increment _ current = pure (succ current)
update Decrement _ current = pure (pred current)

view :: Elm Msg => env -> Int -> View
view _ n = 
  Div <||>
    [ Button <| OnClick (const (command Decrement)) |> [ "-" ]
    , Div <||> [ text n ]
    , Button <| OnClick (const (command Increment)) |> [ "+" ]
    ]
```

Or in the style of `Redux` with global state using [excelsior](/doc/excelsior/latest):

```haskell
import Pure
import Excelsior

data Counter = Counter Int

data CountCmd = Inc | Dec
instance Command Counter CountCmd

count :: Reducer CountCmd Counter
count Inc (Counter n) = Counter (n + 1)
count Dec (Counter n) = Counter (n - 1)

counter :: View
counter = watcher $ \n ->
  Div <||>
    [ Button <| OnClick (\_ -> command Inc) |>
      [ "Increment" ]
    , Div <||> [ text n ]
    , Button <| OnClick (\_ -> command Dec) |>
      [ "Decrement" ]
    ]

main = inject body $
  Div <||>
    [ createStore (Counter 0) [ reducer counter ] [ ]
    , counter
    ]
```

## The Power of Functional Programming

Do you see the declarative view duplication in the four counters above? Let's generalize!

The core of each of the above implementations is a parameterized view.

```haskell
counter current dec inc =
  Div <||>
    [ Button <| OnClick (const dec) |> [ "-" ]
    , Div <||> [ text current ]
    , Button <| OnClick (const inc) |> [ "+" ]
    ]
```

Using the parameterized view, we can implement each of the previous examples.

The [pure-state](/doc/pure-state/latest) approach:

```haskell
main = inject body counter

counter = runPure 0 $ do
  current <- get
  dec <- embedPure (modify pred)
  inc <- embedPure (modify succ)
  return $ counter current dec inc
```

The [pure component](/doc/pure-core/latest/Pure.Data.View/data%20Comp) approach:

```haskell
main = inject body counter

counter = Component $ \self ->
  let
    dec = modify_ self $ \_ -> pred
    inc = modify_ self $ \_ -> succ
  in
    def
      { construct = return 0
      , render = \_ current -> counter current dec inc
      }

```
The [pure-elm](/doc/pure-elm/latest) approach:

```haskell
main = inject body counter

data Cmd = Inc | Dec

counter = run (App [] [] [] 0 update view)

update Inc _ = pure . succ
update Dec _ = pure . pred

view _ current = counter current (command Inc) (command Dec)

```
And the [excelsior](/doc/excelsior/latest) approach:

```haskell
main = inject body $
  Div <||> 
    [ createStore (Counter 0) [ reducer count ] [ ]
    , counter
    ]

count Inc (Counter n) = Counter (n + 1)
count Dec (Counter n) = Counter (n - 1)
 
instance Command Counter CountCmd

counter = watcher $ \n -> counter n (command Inc) (command Dec)

```
While you may not want to generalize a counter like this, it demonstrates the power of Haskell for transforming and simplifying code! By making the view pure and parameterizing it's effectful functionality, you get the benefits of simplified reasoning and easy modification!

## An Extended Example

Here's a simple chat application - frontend and backend. This is not a robust chat application, but it is especially demonstrative.

#### Shared

First, the code shared between the frontend and backend:

```haskell
module Shared where

import Pure
import Pure.WebSocket as WS

type Username = Txt
type Chatter = Txt

mkMessage "Say" [t|(Username,Chatter)|]

serverAPI = api msgs reqs
  where
    msgs = say <:> WS.none
    reqs = WS.none

mkMessage "New" [t|(Username,Chatter,Time)|]

clientAPI = api msgs reqs
  where
    msgs = new <:> WS.none
    reqs = WS.none
```

This declares two APIs:

- a server API that the client can use to send new messages to the server
- a client API that the server can use to send new messages to the browser

For demonstration, the client API includes a Time in the message, otherwise the client and server could use and implement the same API.

> Note only `mkMessage` is used here, which requires only a type of message. For a request/response interface, there is `mkRequest` that takes a single argument function type, used as:
>
> ```haskell
> mkRequest "SomeRequest" [t|SomeRequestData -> SomeResponseData|]
> ```

#### Backend

The backend server.

Every connection `conn` when `executing`, `enacts` the `serverAPI` which listens for new messages with `handleSay`. 

Each message is stored in the application `State` which gets propagated to every connected client, including the sender, when the `server` calls `render`.

The `conn` will `receive` the updated state as a property and send the new message with `notify`.

> NOTE: We have to use a sleep loop to keep the server alive because all components are forked into their own threads leaving nothing running in the main thread once the injection is done.

```haskell
module Main where

import Pure
import Pure.Server
import Pure.WebSocket as WS

import Shared

import Control.Concurrent
import Control.Monad

data State = State (Username,Chatter,Time)

main :: IO ()
main = inject body (server ()) >> sleep
  where
    sleep = forever (threadDelay (6 * 10 ^ 10))

server = Component $ \self -> def
    { construct = return $ State ("server","Hello, World!",0)
    , render = \_ st -> Server "127.0.0.1" 8081 (\ws -> conn self ws st)
    }

conn ref ws = Component $ \self -> def
    { construct = return ()
    , receive = \(State msg) _ -> notify Shared.clientAPI ws new msg
    , executing = enact ws (impl ref) >> activate ws
    }

impl ref = Impl serverAPI msgs reqs
  where
    msgs = handleSay ref <:> WS.none
    reqs = WS.none

handleSay :: Ref () State -> MessageHandler Say
handleSay ref = awaiting $ do
  (user,message) <- acquire
  now <- liftIO time
  liftIO $ modify_ ref $ \_ _ -> State (user,message,now)
```

#### Frontend

The frontend server. 

The `app`, when `executing`, `enacts` the `clientAPI` which listens for new messages with `handleNew`.

Each new message is prepended in the application `State` which gets rendered by the `messages` view function.

A simple stateful view manages the username, the message text, and send events.

> NOTE: If the server goes down, any sends will be buffered and complete when the server comes back up.

```haskell
module Main where

import Pure
import Pure.WebSocket as WS

import Shared

import Data.Maybe

data State = State [(Username,Chatter,Time)]

main :: IO ()
main = clientWS "127.0.0.1" 8081 >>= inject body . app

app :: WebSocket -> View
app = Component $ \self -> def
    { construct = return (State [])
    , executing = do
        ws <- ask self
        _ <- enact ws (Main.client self)
        return ()
    , render = \ws (State ms) ->
        Div <||>
          [ messages ms
          , input ws self
          ]
    }

messages ms =
  Ul <||>
    [ Li <||>
      [ text user
      , "@"
      , toPrettyTime time
      , ": "
      , text message
      ]
    | (user,message,time) <- ms
    ]

input ws app = runPureWithIO (mempty,mempty) $ \ref -> do
  (_,msg) <- getWith ref

  let
    setUsername un = modifyWith ref (\(_,c) -> (un,c))

    setChatter c = modifyWith ref (\(un,_) -> (un,c))

    submit = do
      (user,chatter) <- getWith ref
      let username = if user == mempty then "anon" else user
      putWith ref (user,mempty)
      notify serverAPI ws say (username,chatter)

  return $ chatter setUsername setChatter submit msg

chatter :: (Txt -> IO ()) -> (Txt -> IO ()) -> IO () -> Txt -> View
chatter setUsername setChatter submit msg = 
  Div <||>
    [ Input  <| OnInput (withInput setUsername) . Placeholder "Name"
    , Input  <| OnInput (withInput setChatter)  . Placeholder "Message" . Value msg
    , Button <| OnClick (const submit) |> 
      [ "Send" ]
    ]

client app = Impl clientAPI msgs reqs
  where
    msgs = handleNew app <:> WS.none
    reqs = WS.none

handleNew :: Ref WebSocket State -> MessageHandler New
handleNew app = awaiting $ do
  m <- acquire
  liftIO $ modify_ app $ \_ (State ms) -> State (m:ms)
```

## Ready to Try Pure?

If you're ready to get started with Pure, check out the [Installation guide](/tut/install).
